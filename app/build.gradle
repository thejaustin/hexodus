plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    namespace 'com.hexodus'
    compileSdk 36

    def versionPropsFile = rootProject.file('version.properties')
    def versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    def major = versionProps['version.major'].toInteger()
    def minor = versionProps['version.minor'].toInteger()
    def patch = versionProps['version.patch'].toInteger()

    defaultConfig {
        applicationId "com.hexodus"
        minSdk 26
        targetSdk 36
        versionCode major * 10000 + minor * 100 + patch  // Format: MMmmddd (Major.Minor.Patch)
        versionName "${major}.${minor}.${patch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

}

dependencies {
    // Core Android libraries
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.0'
    implementation 'androidx.lifecycle:lifecycle-service:2.9.0'
    implementation 'androidx.activity:activity-compose:1.10.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.1'
    implementation platform('androidx.compose:compose-bom:2026.02.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'

    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.8.5'

    // Material Design components
    implementation 'com.google.android.material:material:1.12.0'

    // Shizuku integration (from Maven Central)
    implementation 'dev.rikka.shizuku:api:13.1.5'
    implementation 'dev.rikka.shizuku:provider:13.1.5'

    // Window management (for foldables)
    implementation 'androidx.window:window:1.5.1'
    implementation 'androidx.window:window-java:1.5.1'

    // Security
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'

    // Palette for color extraction
    implementation 'androidx.palette:palette-ktx:1.0.0'

    // Permissions (for Compose)
    implementation 'com.google.accompanist:accompanist-permissions:0.34.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2026.02.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}